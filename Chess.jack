
/**
 * >>> Description <<<
 * This class controls the chess game and logic
 * Board consists of 64 32x32 pixel squares with origin at (122,0)
 *    _______________________
 * 1 |__|__|__|__|__|__|__|__|
 * 2 |__|__|__|__|__|__|__|__|
 * 3 |__|__|__|__|__|__|__|__|
 * 4 |__|__|__|__|__|__|__|__|
 * 5 |__|__|__|__|__|__|__|__|
 * 6 |__|__|__|__|__|__|__|__|
 * 7 |__|__|__|__|__|__|__|__|
 * 8 |__|__|__|__|__|__|__|__|
 *    A  B  C  D  E  F  G  H  
 *
 * >>> Controls <<<
 * ARROW KEYS:  control movement between pieces 
 * SPACEBAR:    A piece can be selected to be moved by pressing SPACEBAR
 *              Once selected you can use the arrow keys to the new position 
 *              for selected piece and press Enter again to move to desired 
 *              position.
 * X :          To cancel a selection, press X
 **/

class Chess {
    static int xStart;
    field boolean gameEnd;
    field boolean isClearTurn;
    field boolean inCheck;
    field Marker _curPosMarker;
    field Marker _selPosMarker;

    // Clear pieces
    field Array clearPieces; 
    
    // Stripe pieces
    field Array stripePieces;

    //
    // Ctor / Dtor
    //
    constructor Chess new() {
        var int i, pawnType, rookType, kingType, queenType, bishopType, knightType;
        var Pawn curPawn;      

        let rookType    = ChessPieceHelper.getRookType();
        let pawnType    = ChessPieceHelper.getPawnType();
        let kingType    = ChessPieceHelper.getKingType();
        let queenType   = ChessPieceHelper.getQueenType();
        let bishopType  = ChessPieceHelper.getBishopType();
        let knightType  = ChessPieceHelper.getKnightType();

        let i           = 0;
        let xStart      = 122;
        let gameEnd     = false;
        let inCheck     = false;
        let isClearTurn = true;        

        let _curPosMarker   = Marker.new(Point.new(xStart, 32), false);
        let _selPosMarker   = null;        
        let clearPieces     = Array.new(16);
        let stripePieces    = Array.new(16);

        // init pawns
        while(i < 8) {                    
            let clearPieces[i] = ChessPiece.new(Point.new(xStart + (32*i), 32), true, pawnType);            
            let stripePieces[i] = ChessPiece.new(Point.new(xStart + (32*i), 192), false, pawnType); 
            let i = i + 1;
        }        

        // init rooks      
        let clearPieces[8]  = ChessPiece.new(Point.new(xStart, 0), true, rookType);
        let stripePieces[8] = ChessPiece.new(Point.new(xStart, 224), false, rookType);
        let clearPieces[9]  = ChessPiece.new(Point.new(xStart + (32*7), 0), true, rookType);
        let stripePieces[9] = ChessPiece.new(Point.new(xStart + (32*7), 224), false, rookType);

        // init knights
        let clearPieces[10]     = ChessPiece.new(Point.new(xStart + 32, 0), true, knightType);
        let clearPieces[11]     = ChessPiece.new(Point.new(xStart + (32* 6), 0), true, knightType);
        let stripePieces[10]    = ChessPiece.new(Point.new(xStart + 32, 224), false, knightType);
        let stripePieces[11]    = ChessPiece.new(Point.new(xStart + (32* 6), 224), false, knightType);


        // init bishops
        let clearPieces[12]     = ChessPiece.new(Point.new(xStart + 64, 0), true, bishopType);
        let clearPieces[13]     = ChessPiece.new(Point.new(xStart + (32 * 5), 0), true, bishopType); 
        let stripePieces[12]    = ChessPiece.new(Point.new(xStart + 64, 224), false, bishopType);
        let stripePieces[13]    = ChessPiece.new(Point.new(xStart + (32 * 5), 224), false, bishopType);

        // init queens
        let clearPieces[14]     = ChessPiece.new(Point.new(xStart + 128, 0), true, queenType);
        let stripePieces[14]    = ChessPiece.new(Point.new(xStart + 128, 224), false, queenType);

        // init kings     
        let clearPieces[15]     = ChessPiece.new(Point.new(xStart + 96, 0), true, kingType);    
        let stripePieces[15]    = ChessPiece.new(Point.new(xStart + 96, 224), false, kingType);

        return this;
    }

    method void dispose() {
        do clearPieces.dispose();
        do stripePieces.dispose();        
        do Memory.deAlloc(this);
        return;
    }

    //
    // Getters/Setters
    //
    method Array getClearPiece() {
        return clearPieces;
    }

    method Array getStripePieces() {
        return stripePieces;
    }

    function int getXStart() {
        return xStart;
    }

    //
    // Methods
    // 
    method void run() {
        var char key;        
        var ChessPiece curPiece;    
        var ChessPiece selectedPiece; 
        var Point curPos; 
        var int i;  

        do drawBoard();
        do drawPieces();

        let key = 0;
        let curPiece = clearPieces[0];
        let selectedPiece = null;
        
        do _curPosMarker.draw();

        // Output Controls
        do Output.moveCursor(1,48);
        do Output.printString("SPACE:");        
        do Output.moveCursor(3,48);
        do Output.printString(" Select/Move");
        do Output.moveCursor(5, 48);
        do Output.printString("ARROW KEYS:");
        do Output.moveCursor(7, 49);
        do Output.printString("Move piece");
        do Output.moveCursor(9, 48);
        do Output.printString("X:");
        do Output.moveCursor(11, 49);
        do Output.printString("Cancel select");
        do Output.moveCursor(13, 48);
        do Output.printString("Q:");
        do Output.moveCursor(15, 49);
        do Output.printString("Quit/Exit");

        // GAME LOOP
        while(~gameEnd) {
            // Output whose turn it is
            do Screen.setColor(false);
            do Screen.drawRectangle(0,0,xStart-1, 80);
            do Output.moveCursor(1,1);
            do Output.printString("Turn: ");            
            
            if(isClearTurn) {
                do Output.printString("Clear");
            }
            else {
                do Output.printString("Stripe");
            }   
            if(inCheck) {
                do Output.moveCursor(4,2);
                do Output.printString("CHECK!");
            }         

            let curPos = _curPosMarker.getPosition();
            while (key = 0) {
                let key = Keyboard.keyPressed();                
            }   

            if (key = 81)  {        // q key
                let gameEnd = true; 
            }     
            if (key = 131) {        // up arrow  
                if(~(curPos.getY() = 0)) {
                    do ChessPieceHelper.drawSquare(curPos);
                    do drawBorder();
                    let curPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, curPos);
                    if(~(curPiece = null)) {
                        do curPiece.draw(ChessPieceHelper.onLight(curPiece.getPosition()));
                    }

                    do _curPosMarker.moveUp(); 
                    if(~(_selPosMarker = null)) {
                        do _selPosMarker.draw();
                    }
                }
            }   
            if (key = 133) {        // down arrow   
                if(~(curPos.getY() = (32*7))) {          
                    do ChessPieceHelper.drawSquare(curPos);
                    do drawBorder();
                    let curPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, curPos);
                    if(~(curPiece = null)) {
                        do curPiece.draw(ChessPieceHelper.onLight(curPiece.getPosition()));
                    }
                    do _curPosMarker.moveDown(); 
                    if(~(_selPosMarker = null)) {
                        do _selPosMarker.draw();
                    }
                }
            }   
            if (key = 130) {        // left arrow
                if(~(curPos.getX() = xStart)) {
                    do ChessPieceHelper.drawSquare(curPos);
                    do drawBorder();
                    let curPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, curPos);
                    if(~(curPiece = null)) {
                        do curPiece.draw(ChessPieceHelper.onLight(curPiece.getPosition()));
                    }
                    do _curPosMarker.moveLeft();
                    if(~(_selPosMarker = null)) {
                        do _selPosMarker.draw();
                    }
                }
            }   
            if (key = 132) {        // right arrow
                if(~(curPos.getX() = (xStart + (32*7)))) {
                    do ChessPieceHelper.drawSquare(curPos);
                    do drawBorder();
                    let curPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, curPos);
                    if(~(curPiece = null)) {
                        do curPiece.draw(ChessPieceHelper.onLight(curPiece.getPosition()));
                    }
                    do _curPosMarker.moveRight(); 
                    if(~(_selPosMarker = null)) {
                        do _selPosMarker.draw();
                    }
                }
            }   
            if(key = 32) {          // Space bar
                if(selectedPiece = null) {
                    let selectedPiece = _curPosMarker.select(isClearTurn, clearPieces, stripePieces);
                    if(~(selectedPiece = null)) {
                        let _selPosMarker = Marker.new(selectedPiece.getPosition(), true);
                        do _selPosMarker.draw();
                    }
                }
                else {
                    if(~ChessPieceHelper.comparePosition(curPos, selectedPiece.getPosition())) {                        
                        if(ChessPieceHelper.canMove(selectedPiece, curPos, clearPieces, stripePieces)) {
                            do selectedPiece.setPosition(curPos);

                            if(checkForCheck(selectedPiece.getIsClear())) {
                                do selectedPiece.setPosition(_selPosMarker.getPosition());
                            }
                            else {
                                do ChessPieceHelper.drawSquare(_selPosMarker.getPosition());
                                do drawBorder();
                                //do selectedPiece.setPosition(curPos);
                                do selectedPiece.draw(ChessPieceHelper.onLight(selectedPiece.getPosition()));

                                // Check for check
                                if(checkForCheck(~selectedPiece.getIsClear())) {
                                    let inCheck = true;
                                } else {
                                    let inCheck = false;
                                }

                                do _curPosMarker.draw();                            
                                let selectedPiece = null;
                                do _selPosMarker.dispose();
                                let _selPosMarker = null;

                                let isClearTurn = ~isClearTurn;
                            }
                        }
                    }
                }                
            }
            if (key = 88)  {        // X key 
                if(~(selectedPiece = null)) {
                    do ChessPieceHelper.drawSquare(selectedPiece.getPosition());
                    do selectedPiece.draw(ChessPieceHelper.onLight(selectedPiece.getPosition()));
                    //do selectedPiece.dispose();
                    let selectedPiece = null;                
                    do _selPosMarker.dispose();
                    let _selPosMarker = null;
                }
            }

            // reset
            let key = 0;
            do Sys.wait(150);
        }
        return;
    }

    method boolean checkForCheck(boolean isClearKing) {
        var Array attPieces;
        var ChessPiece curPiece;
        var int i;

        let i = 0;        
        if(isClearKing) {
            let attPieces = stripePieces;            
        }
        else {
            let attPieces = clearPieces;
        }

        while(i < 16) {
            let curPiece = attPieces[i];
            if(ChessPieceHelper.checkForCheck(curPiece, clearPieces, stripePieces)) {
                return true;
            }
            let i = i + 1;
        }

        return false;
    }

    method void drawPieces() {
        var int i;              
        var ChessPiece curPiece;

        let i = 0;
        
        while(i < 16) {
            let curPiece = clearPieces[i];
            if(curPiece.getIsAlive()) {
                do ChessPieceDrawer.draw(curPiece, ChessPieceHelper.onLight(curPiece.getPosition()));
            }

            let curPiece = stripePieces[i];
            if(curPiece.getIsAlive()) {
                do ChessPieceDrawer.draw(curPiece, ChessPieceHelper.onLight(curPiece.getPosition()));
            }
            let i = i + 1;            
        }

        return;
    }
    
    method void drawBoard() {
        var int x, y, curRow, curCol;
        var int squareSize;
        var boolean color;

        let curCol      = 0;
        let curRow      = 0;
        let color       = true;
        let squareSize  = 32;
        
        // Draw squares
        while(curRow < 8) {
            while(curCol < 8) {                
                do Screen.setColor(color);
                let color = ~color;

                let x = xStart + (curCol * squareSize);
                let y = curRow * squareSize;
                do Screen.drawRectangle(x, y, x + squareSize, y + squareSize - 1);

                let curCol = curCol + 1;
            }
            let curRow = curRow + 1;
            let curCol = 0;
            let color = ~color;
        }        

        // Draw border
        do drawBorder();

        return;
    }

    method void drawBorder() {
        do Screen.setColor(true);
        do Screen.drawLine(xStart, 0, xStart + 256, 0);
        do Screen.drawLine(xStart, 0, xStart, 255);
        do Screen.drawLine(xStart, 255, xStart + 256, 255);
        do Screen.drawLine(xStart + 256, 0, xStart + 256, 255);
        return;
    }

    
    
}