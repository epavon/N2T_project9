class ChessPieceHelper {

	function ChessPiece getByPosition(Array clearPieces, Array stripePieces, Point position) {
		var int i;
        var ChessPiece curPiece;
        var Point curPiecePosition;

        let i = 0;

        while(i < 16) {        
        	let curPiece = clearPieces[i];                
            let curPiecePosition = curPiece.getPosition();
			if(curPiece.getIsAlive()) {
            	if(curPiecePosition.getX() = position.getX() ) {
            	    if( curPiecePosition.getY() = position.getY()) {
            	        return curPiece;
            	    }
            	}
			}

			let curPiece = stripePieces[i];
			let curPiecePosition = curPiece.getPosition();
			if(curPiece.getIsAlive()) {
            	if(curPiecePosition.getX() = position.getX() ) {
            	    if( curPiecePosition.getY() = position.getY()) {
            	        return curPiece;
            	    }
            	}
			}

            let i = i + 1;
        }

        return null;	
	}

	function void drawPieceInPos(Point position, Array clearPieces, Array stripePieces) {
		var ChessPiece piece;		
		let piece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, position);

		if(~(piece = null)) {
			do piece.draw(ChessPieceHelper.onLight(position));
		}

		return;
	}

	function boolean onLight(Point position) {
		var int x, y;
		let x = position.getX() - 122;
		let y = position.getY();		
		 
		if(MathHelper.modulo(y /32, 2) = 0) {
			if(MathHelper.modulo(x/32, 2) = 1) {
				return true;
			}
		}

		if(MathHelper.modulo(y/32, 2) = 1) {
			if(MathHelper.modulo(x/32, 2) = 0) {
				return true;
			}
		}

		return false;
	}

	function boolean comparePosition(Point pos1, Point pos2) {
		if(pos1.getX() = pos2.getX()) {
			if(pos1.getY() = pos2.getY()) {
				return true;
			}
		}
		return false;
	}

	function boolean isPieceInPos(Point position, Array clearPieces, Array stripePieces) {
		var ChessPiece pieceInDesiredPos;
		let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, position);

		if(pieceInDesiredPos = null) {
			return true;
		}
		return false;
	}


	// @TODO: Do for all pieces, take into account other pieces..
	function boolean canMove(ChessPiece piece, Point desiredPos, Array clearPieces, Array stripePieces) {
		var Point piecePos;
		let piecePos = piece.getPosition();
		
		if(piece.getType() = ChessPieceHelper.getPawnType()) {
			return ChessPieceHelper.canPawnMove(piece, desiredPos, clearPieces, stripePieces, piece.getInInitialPos());
		}
		if(piece.getType() = ChessPieceHelper.getRookType()) {
			return ChessPieceHelper.canRookMove(piece, desiredPos, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getKnightType()) {
			return ChessPieceHelper.canKnightMove(piece, desiredPos, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getBishopType()) {
			return ChessPieceHelper.canBishopMove(piece, desiredPos, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getQueenType()) {
			return ChessPieceHelper.canQueenMove(piece, desiredPos, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getKingType()) {
			return ChessPieceHelper.canKingMove(piece, desiredPos, clearPieces, stripePieces);
		}

		return true;
	}

	function boolean canPawnMove(ChessPiece pawnPiece, Point desiredPos, Array clearPieces, Array stripePieces, boolean inInitialPos) {
		var ChessPiece pieceInDesiredPos;
		var Point piecePos, otherPos;
		var int posDiffCoeff;
		
		let piecePos = pawnPiece.getPosition();		
		if(pawnPiece.getIsClear()) {
			let posDiffCoeff = 1;
		} else {
			let posDiffCoeff = -1;
		}

		if(desiredPos.getX() = piecePos.getX()) {	//  VERTICAL MOVEMENT
			if(desiredPos.getY() - piecePos.getY() = (32 * posDiffCoeff) ) {
				let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
				if(pieceInDesiredPos = null) {
					return true;
				}
			}
			else {
				if(desiredPos.getY() - piecePos.getY() = (64* posDiffCoeff)) {
					if(inInitialPos) {
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
						if(pieceInDesiredPos = null) {
							let otherPos = Point.new(desiredPos.getX(), desiredPos.getY()-(32 * posDiffCoeff));
							let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, otherPos);
							if(pieceInDesiredPos = null) {
								return true;
							}
						}
					}
				}
			}
		}
		else {										// DIAGONAL MOVEMENT
			if(MathHelper.abs(desiredPos.getX() - piecePos.getX()) = 32) {
				if(desiredPos.getY() - piecePos.getY() = (32 * posDiffCoeff)) {
					let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
					if(~(pieceInDesiredPos = null)) {
						do pieceInDesiredPos.setIsAlive(false);
						return true;
					} 
					else { // handle tricky case of opponent pawn moving past two spaces

					}
				}
			}
		}
		
		return false;
		
	}

	function boolean canRookMove(ChessPiece piece, Point desiredPos, Array clearPieces, Array stripePieces) {
		var ChessPiece pieceInDesiredPos;	
		var Point piecePos;
		var int i;

		let piecePos = piece.getPosition();				

		if(desiredPos.getX() = piecePos.getX()) {
			if(desiredPos.getY() > piecePos.getY()) {
				let i = piecePos.getY() + 32;
				while(i < desiredPos.getY()) {
					let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(desiredPos.getX(), i));
					if(~(pieceInDesiredPos = null)) {
						return false;
					}
					let i = i + 32;
				}				
			}
			else {
				let i = piecePos.getY() - 32;
				while(i > desiredPos.getY()) {
					let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(desiredPos.getX(), i));
					if(~(pieceInDesiredPos = null)) {
						return false;
					}
					let i = i - 32;
				}				
			}

			let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
			if(pieceInDesiredPos = null) {
				return true;
			}
			else {
				if(~(piece.getIsClear() = pieceInDesiredPos.getIsClear())) {
					do pieceInDesiredPos.setIsAlive(false);
					return true;
				}
			}
		}
		else {
			if(desiredPos.getY() = piecePos.getY()) {
				if(desiredPos.getX() > piecePos.getX()) {
					let i = piecePos.getX() + 32;
					while(i < desiredPos.getX()) {
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(i,desiredPos.getY()));
						if(~(pieceInDesiredPos = null)) {
							return false;
						}
						let i = i + 32;
					}				
				}
				else {
					let i = piecePos.getX() - 32;
					while(i > desiredPos.getX()) {
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(i,desiredPos.getY()));
						if(~(pieceInDesiredPos = null)) {
							return false;
						}
						let i = i - 32;
					}				
				}

				let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
				if(pieceInDesiredPos = null) {
					return true;
				}
				else {
					if(~(piece.getIsClear() = pieceInDesiredPos.getIsClear())) {
						do pieceInDesiredPos.setIsAlive(false);
						return true;
					}
				}
			}
		}		 

		return false;		
	}

	function boolean canKnightMove(ChessPiece piece, Point desiredPos, Array clearPieces, Array stripePieces) {
		var Point piecePos;
		var ChessPiece pieceInDesiredPos;

		let pieceInDesiredPos 	= ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
		let piecePos 			= piece.getPosition();

		if(MathHelper.abs(piecePos.getX() - desiredPos.getX()) = 32) {
			if(MathHelper.abs(piecePos.getY() - desiredPos.getY()) = 64) {
				if(~(pieceInDesiredPos = null)) {
					if(~(pieceInDesiredPos.getIsClear() = piece.getIsClear())) {
						do pieceInDesiredPos.setIsAlive(false);
						return true;
					}
				}
				else {
					return true;
				}				
			}
		}
		else {
			if(MathHelper.abs(piecePos.getX() - desiredPos.getX()) = 64) {
				if(MathHelper.abs(piecePos.getY() - desiredPos.getY()) = 32) {
					if(~(pieceInDesiredPos = null)) {
						if(~(pieceInDesiredPos.getIsClear() = piece.getIsClear())) {
							do pieceInDesiredPos.setIsAlive(false);
							return true;
						}
					}
					else {
						return true;
					}							
				}
			}
		}

		return false;
		
	}

	function boolean canBishopMove(ChessPiece piece, Point desiredPos, Array clearPieces, Array stripePieces) {
		var Point piecePos, nextPos;
		var ChessPiece pieceInDesiredPos;
		var int i, j, diff;

		let piecePos = piece.getPosition();
		
		if(MathHelper.abs(piecePos.getX() - desiredPos.getX()) = MathHelper.abs(piecePos.getY() - desiredPos.getY()) ) {
			if(piecePos.getY() > desiredPos.getY()) {
				let i = 1;
				let diff = (piecePos.getY() - desiredPos.getY()) / 32;

				if(piecePos.getX() > desiredPos.getX()) {					
					while(i < diff) {
						let nextPos = Point.new(piecePos.getX() - (i*32),piecePos.getY() - (i*32));
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, nextPos);
						if(~(pieceInDesiredPos = null)) {
							return false;
						}
						let i = i + 1;
					}
				}
				else {
					while(i < diff) {
						let nextPos = Point.new(piecePos.getX() + (i*32),piecePos.getY() - (i*32));
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, nextPos);
						if(~(pieceInDesiredPos = null)) {
							return false;
						}
						let i = i + 1;
					}
				}
			}
			else {
				let i = 1;
				let diff = (desiredPos.getY() - piecePos.getY()) / 32;

				if(piecePos.getX() > desiredPos.getX()) {
					while(i < diff) {
						let nextPos = Point.new(piecePos.getX() - (i*32),piecePos.getY() + (i*32));
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, nextPos);
						if(~(pieceInDesiredPos = null)) {
							return false;
						}
						let i = i + 1;
					}
				}
				else {
					while(i < diff) {
						let nextPos = Point.new(piecePos.getX() + (i*32),piecePos.getY() + (i*32));
						let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, nextPos);
						if(~(pieceInDesiredPos = null)) {
							return false;
						}
						let i = i + 1;
					}
				}
			}

			let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
			if(pieceInDesiredPos = null) {
				return true;
			}
			else {
				if(~(piece.getIsClear() = pieceInDesiredPos.getIsClear())) {
					do pieceInDesiredPos.setIsAlive(false);
					return true;
				}
			}
		}		

		return false;		
	}

	function boolean canQueenMove(ChessPiece piece, Point desiredPos, Array clearPieces, Array stripePieces) {		
		if(ChessPieceHelper.canRookMove(piece, desiredPos, clearPieces, stripePieces) |
			ChessPieceHelper.canBishopMove(piece, desiredPos, clearPieces, stripePieces) ) {
				return true;
		}

		return false;
		
	}

	function boolean canKingMove(ChessPiece piece, Point desiredPos, Array clearPieces, Array stripePieces) {
		var int desiredPosX, desiredPosY;
		var Point piecePos;
		var ChessPiece pieceInDesiredPos;
		var ChessPiece rookPiece;

		let desiredPosX = desiredPos.getX();
		let desiredPosY = desiredPos.getY();
		let piecePos = piece.getPosition(); 
		let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);

		if(piecePos.getX() = desiredPos.getX()) {
			if(MathHelper.abs(piecePos.getY() - desiredPos.getY()) = 32) {
				if(~(pieceInDesiredPos = null)) {
					if(~(pieceInDesiredPos.getIsClear() = piece.getIsClear())) {
						return true;
					}
				}
				else {
					return true;
				}
			}
		}

		if(piecePos.getY() = desiredPos.getY()) {
			if(MathHelper.abs(piecePos.getX() - desiredPos.getX()) = 32) {
				if(~(pieceInDesiredPos = null)) {
					if(~(pieceInDesiredPos.getIsClear() = piece.getIsClear())) {
						do pieceInDesiredPos.setIsAlive(false);
						return true;
					}
				}
				else {
					return true;
				}
			}
		}

		if( MathHelper.abs(piecePos.getX() - desiredPos.getX()) = 32 ) {
			if(MathHelper.abs(piecePos.getY() - desiredPos.getY()) = 32) {
				if(~(pieceInDesiredPos = null)) {
					if(~(pieceInDesiredPos.getIsClear() = piece.getIsClear())) {
						do pieceInDesiredPos.setIsAlive(false);
						return true;
					}
				}
				else {
					return true;
				}
			}
		}

		// Handle castling
		if(desiredPosX = (piecePos.getX() - 64)) {
			let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX() - 32, piecePos.getY()));
			if(pieceInDesiredPos = null) {
				let pieceInDesiredPos = ChessPieceHelper.getByPosition(clearPieces, stripePieces, desiredPos);
				if(pieceInDesiredPos = null) {
					let rookPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(desiredPosX - 32, desiredPosY));
					if(~(rookPiece = null)) {
						if(rookPiece.getType() = ChessPieceHelper.getRookType()) {
							if(rookPiece.getInInitialPos() & piece.getInInitialPos()) {
								do ChessPieceHelper.drawSquare(rookPiece.getPosition());
								do rookPiece.setPosition(Point.new(desiredPosX + 32, desiredPosY));
								do rookPiece.draw(ChessPieceHelper.onLight(rookPiece.getPosition()));
								return true;
							}
						}
					}
				}
			}
		}

		return false;
	}

	function void drawSquare(Point position) {
        var int x, y;
        let x = position.getX();
        let y = position.getY();
        do Screen.setColor(~ChessPieceHelper.onLight(position));
        do Screen.drawRectangle(x, y, x + 32, y + 32 - 1);
        return;
    }

	function void drawBorder() {
        do Screen.setColor(true);
        do Screen.drawLine(Chess.getXStart(), 0, Chess.getXStart() + 256, 0);
        do Screen.drawLine(Chess.getXStart(), 0, Chess.getXStart(), 255);
        do Screen.drawLine(Chess.getXStart(), 255, Chess.getXStart() + 256, 255);
        do Screen.drawLine(Chess.getXStart() + 256, 0, Chess.getXStart() + 256, 255);
        return;
    }

	function boolean checkForCheck(ChessPiece piece, Array clearPieces, Array stripePieces) {
		if(piece.getType() = ChessPieceHelper.getPawnType()) {
			return ChessPieceHelper.checkPawnForCheck(piece, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getRookType()) {
			return ChessPieceHelper.checkRookForCheck(piece, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getKnightType()) {
			return ChessPieceHelper.checkKnightForCheck(piece, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getBishopType()) {
			return ChessPieceHelper.checkBishopForCheck(piece, clearPieces, stripePieces);
		}
		if(piece.getType() = ChessPieceHelper.getQueenType()) {
			return ChessPieceHelper.checkQueenForCheck(piece, clearPieces, stripePieces);
		}
		
		return false;
	}

	function boolean checkPawnForCheck(ChessPiece piece, Array clearPieces, Array stripePieces) {
		var ChessPiece opposingKing;
		var Point oppKingPos;
		var Point piecePos;

		let piecePos = piece.getPosition();
		if(piece.getIsClear()) {
			let opposingKing = stripePieces[15];
			let oppKingPos = opposingKing.getPosition();

			if( MathHelper.abs(piecePos.getX() - oppKingPos.getX()) = 32 ) {
				if(oppKingPos.getY() - piecePos.getY() = 32) {
					return true;
				}
			}
		}
		else {
			let opposingKing = clearPieces[15];
			let oppKingPos = opposingKing.getPosition();

			if( MathHelper.abs(piecePos.getX() - oppKingPos.getX()) = 32 ) {
				if(piecePos.getY() - oppKingPos.getY() = 32) {
					return true;
				}
			}
		}		

		return false;
	}

	function boolean checkRookForCheck(ChessPiece piece, Array clearPieces, Array stripePieces) {
		var ChessPiece nextPiece;
		var ChessPiece oppKing;
		var Point piecePos;
		var Point oppKingPos;
		var int i;

		let piecePos = piece.getPosition();		
		if(piece.getIsClear()) {
			let oppKing = stripePieces[15];
			let oppKingPos = oppKing.getPosition();			
		}
		else {
			let oppKing = clearPieces[15];
			let oppKingPos = oppKing.getPosition();			
		}	

		if(piecePos.getX() = oppKingPos.getX()) {			
			if(piecePos.getY() > oppKingPos.getY()) {
				let i = piecePos.getY() - 32;
				while(i > oppKingPos.getY()) {
					let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX(), i));
					if(~(nextPiece = null)) {
						return false;
					}
					let i = i - 32;
				}
				return true;
			}
			else {
				let i = piecePos.getY() + 32;
				while(i < oppKingPos.getY()) {
					let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX(), i));
					if(~(nextPiece = null)) {
						return false;
					}
					let i = i + 32;
				}
				return true;
			}
		} 
		else {
			if(piecePos.getY() = oppKingPos.getY()) {				
				if(piecePos.getX() > oppKingPos.getX()) {
					let i = piecePos.getX() - 32;
					while(i > oppKingPos.getX()) {
						let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(i, piecePos.getY()));
						if(~(nextPiece = null)) {
							return false;
						}
						let i = i - 32;
					}
					return true;
				}
				else {
					let i = piecePos.getX() + 32;
					while(i < oppKingPos.getX()) {
						let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(i, piecePos.getY()));
						if(~(nextPiece = null)) {
							return false;
						}
						let i = i + 32;
					}
					return true;
				}
			}
		}

		return false;
	}

	function boolean checkKnightForCheck(ChessPiece piece,Array clearPieces, Array stripePieces) {
		var ChessPiece oppKing;		
		var Point oppKingPos;
		var Point piecePos;		

		let piecePos = piece.getPosition();
		if(piece.getIsClear()) {
			let oppKing = stripePieces[15];
			let oppKingPos = oppKing.getPosition();			
		}
		else {
			let oppKing = clearPieces[15];
			let oppKingPos = oppKing.getPosition();			
		}		

		if(MathHelper.abs(piecePos.getX() - oppKingPos.getX()) = 32) {
			if(MathHelper.abs(piecePos.getY() - oppKingPos.getY()) = 64) {
				return true;
			}
		}
		else {
			if(MathHelper.abs(piecePos.getX() - oppKingPos.getX()) = 64) {
				if(MathHelper.abs(piecePos.getY() - oppKingPos.getY()) = 32) {
					return true;
				}
			}
		}

		return false;
	}

	function boolean checkBishopForCheck(ChessPiece piece, Array clearPieces, Array stripePieces) {
		var ChessPiece nextPiece;
		var ChessPiece oppKing;
		var Point piecePos;
		var Point oppKingPos;
		var int i, n;

		let piecePos = piece.getPosition();		
		if(piece.getIsClear()) {
			let oppKing = stripePieces[15];
			let oppKingPos = oppKing.getPosition();			
		}
		else {
			let oppKing = clearPieces[15];
			let oppKingPos = oppKing.getPosition();			
		}	

		if(MathHelper.abs(piecePos.getX() - oppKingPos.getX()) = MathHelper.abs(piecePos.getY() - oppKingPos.getY()) ) {
			if(piecePos.getY() > oppKingPos.getY()) {
				if(piecePos.getX() > oppKingPos.getX()) {
					let i = 1;
					while((piecePos.getX() - (32*i)) > oppKingPos.getX()) {
						let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX() - (32*i), piecePos.getY() - (32*i)));
						if(~(nextPiece = null)) {
							return false;			
						}
						let i = i + 1;
					}
					return true;
				}
				else {
					let i = 1;
					while((piecePos.getX() + (32*i)) < oppKingPos.getX()) {
						let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX() + (32*i), piecePos.getY() - (32*i)));
						if(~(nextPiece = null)) {
							return false;			
						}
						let i = i + 1;
					}
					return true;
				}
			}
			else {
				if(piecePos.getX() > oppKingPos.getX()) {
					let i = 1;
					while((piecePos.getX() - (32*i)) > oppKingPos.getX()) {
						let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX() - (32*i), piecePos.getY() + (32*i)));
						if(~(nextPiece = null)) {
							return false;			
						}
						let i = i + 1;
					}
					return true;
				}
				else {
					let i = 1;
					while((piecePos.getX() + (32*i)) < oppKingPos.getX()) {
						let nextPiece = ChessPieceHelper.getByPosition(clearPieces, stripePieces, Point.new(piecePos.getX() + (32*i), piecePos.getY() + (32*i)));
						if(~(nextPiece = null)) {
							return false;			
						}
						let i = i + 1;
					}
					return true;
				}
			}
		}

		return false;
	}

	function boolean checkQueenForCheck(ChessPiece piece, Array clearPieces, Array stripePieces) {
		if(ChessPieceHelper.checkBishopForCheck(piece, clearPieces, stripePieces) | 
			ChessPieceHelper.checkRookForCheck(piece, clearPieces, stripePieces)) {
			return true;
		}

		return false;
	}
	

	function int getPawnType() {
		return 5;
	}
    
	function int getKingType() {
		return 0;
	}

	function int getQueenType() {
		return 1;
	}

	function int getBishopType() {
		return 2;
	}

	function int getRookType() {
		return 4;
	}

	function int getKnightType() {
		return 3;
	}

}